ORGANIZATION_BLOCK OB 100
BEGIN
	// Check initial DB 1 values
	AUF		DB 1
	L		DBW 0
	__ASSERT==	__ACCU 1,	1337
	L		DBD 2
	__ASSERT==	__ACCU 1,	DW#16#DEADBEEF
	L		DBW 6
	__ASSERT==	__ACCU 1,	W#16#0102
	L		DBB 8
	__ASSERT==	__ACCU 1,	3
	L		DBB 9
	__ASSERT==	__ACCU 1,	0
	L		DBW 10
	__ASSERT==	__ACCU 1,	42

	// Check initial DB 2 values
	AUF		DB 2
	L		DBW 0
	__ASSERT==	__ACCU 1,	123
	L		DBW 2
	__ASSERT==	__ACCU 1,	456
	L		DBB 4
	__ASSERT==	__ACCU 1,	1
	L		DBB 5
	__ASSERT==	__ACCU 1,	0
	L		DBD 6
	__ASSERT==	__ACCU 1,	DW#16#77777777

	SET
	=		M 0.0
END_ORGANIZATION_BLOCK



ORGANIZATION_BLOCK OB 1
BEGIN
	// OB 100 execution sanity check
	__ASSERT==	M 0.0,		1

	// Modify the DB 1
	AUF		DB 1
	L		9876
	T		DBW 6
	L		5432
	T		DBW 10
	CLR
	=		DBX 8.0
	SET
	=		DBX 8.2

	// Check the DB 1 values
	L		DBW 0
	__ASSERT==	__ACCU 1,	1337
	L		DBD 2
	__ASSERT==	__ACCU 1,	DW#16#DEADBEEF
	L		DBW 6
	__ASSERT==	__ACCU 1,	9876
	L		DBB 8
	__ASSERT==	__ACCU 1,	6
	L		DBB 9
	__ASSERT==	__ACCU 1,	0
	L		DBW 10
	__ASSERT==	__ACCU 1,	5432


	// FB 1 call test
	CLR
	=		M 18.0
	CALL		FB 1, DB 2 (
		INPUT_VAR	:= 8877,
		OUTPUT_VAR	:= MW 16,
		INOUT_VAR	:= M 18.0
	)
	__ASSERT==	MW 16,		4433
	__ASSERT==	M 18.0,		1


	// FC 1 call test
	L		1616
	T		MW 16
	L		2020
	T		MW 20
	CALL		FC 1 (
		INPUT_WORD	:= MW 16,
		OUTPUT_BOOL0	:= M 18.0,
		OUTPUT_BOOL1	:= M 18.1,
		INOUT_INT	:= MW 20
	)
	__ASSERT==	M 18.0,		1
	__ASSERT==	M 18.1,		0
END_ORGANIZATION_BLOCK



FUNCTION FC 1: VOID
	VAR_INPUT
		INPUT_WORD	: WORD
	END_VAR
	VAR_OUTPUT
		OUTPUT_BOOL0	: BOOL
		OUTPUT_BOOL1	: BOOL
	END_VAR
	VAR_IN_OUT
		INOUT_INT	: INT
	END_VAR
BEGIN
	L		#INPUT_WORD
	__ASSERT==	__ACCU 1,	1616

	L		#INOUT_INT
	__ASSERT==	__ACCU 1,	2020

	SET
	=		#OUTPUT_BOOL0
	CLR
	=		#OUTPUT_BOOL1
END_FUNCTION



FUNCTION_BLOCK FB 1
	VAR_INPUT
		INPUT_VAR	: INT
	END_VAR
	VAR_OUTPUT
		OUTPUT_VAR	: INT
	END_VAR
	VAR_IN_OUT
		INOUT_VAR	: BOOL
	END_VAR
	VAR
		STAT_VAR	: DWORD
	END_VAR
BEGIN
	L		#INPUT_VAR
	__ASSERT==	__ACCU 1,	8877

	L		4433
	T		#OUTPUT_VAR

	U		#INOUT_VAR
	__ASSERT==	__STW VKE,	0
	SET
	=		#INOUT_VAR

	L		#STAT_VAR
	__ASSERT==	__ACCU 1,	DW#16#77777777
END_FUNCTION_BLOCK



DATA_BLOCK DB 1
TITLE = global DB
STRUCT
	VARIABLE0	: INT
	VARIABLE1	: DWORD
	VARIABLE2	: WORD
	VARIABLE3	: BOOL
	VARIABLE4	: BOOL
	VARIABLE5	: BOOL
	VARIABLE6	: INT
END_STRUCT
BEGIN
	VARIABLE0	:= 1337
	VARIABLE1	:= DW#16#DEADBEEF
	VARIABLE2	:= B#(1, 2)
	VARIABLE3	:= True
	VARIABLE4	:= True
	VARIABLE5	:= False
	VARIABLE6	:= 42
END_DATA_BLOCK



DATA_BLOCK DB 2
TITLE = instance DB for FB 1
FB 1
BEGIN
	INPUT_VAR	:= 123
	OUTPUT_VAR	:= 456
	INOUT_VAR	:= True
	STAT_VAR	:= DW#16#77777777
END_DATA_BLOCK
